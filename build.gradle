plugins {
    id "maven-publish"
    id "signing"
    id "idea"
    id "org.springframework.boot" version "1.5.20.RELEASE" apply(false)
    id "java-library"
}

allprojects {
    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenCentral()
    }
}

subprojects { project ->

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:1.5.20.RELEASE"
        }

        dependencies {
            dependency "com.google.code.findbugs:jsr305:1.3.9"
            dependency "org.projectlombok:lombok:1.18.6"
            dependency "io.github.benas:random-beans:3.7.0"
            dependency "org.unitils:unitils-core:3.4.2"
            dependency "mysql:mysql-connector-java:5.1.39"
            dependency "com.h2database:h2:1.4.196"
            dependency "org.mybatis:mybatis:3.4.6"
            dependency "org.mybatis:mybatis-spring:1.3.0"
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2"
            dependency "org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.1.2"
            dependency "com.github.pagehelper:pagehelper:5.1.2"
            dependency "com.github.pagehelper:pagehelper-spring-boot-starter:1.2.3"
            dependency "org.aspectj:aspectjweaver:1.9.0"
            dependency 'com.github.peacetrue:peacetrue-core:1.0-SNAPSHOT'
            dependency 'com.github.peacetrue:peacetrue-result:1.0-SNAPSHOT'
            dependency 'com.github.peacetrue:spring-data-commons-page:1.0-RELEASE'
            dependency 'com.github.peacetrue:peacetrue-validation:1.0-SNAPSHOT'
            dependency 'javax.validation:validation-api:1.1.0.Final'
        }
    }

    group "com.github.peacetrue.template"
    version "1.0-SNAPSHOT"
    description "模板结构"

    apply plugin: "java"
    apply plugin: "java-library"
    sourceCompatibility = 1.8

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        implementation("com.google.code.findbugs:jsr305")

        testImplementation "junit:junit"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
    }

    if (name.endsWith("sample") || name.endsWith("ui")) return;

    task sourcesJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        archiveClassifier = "javadoc"
        from javadoc.destinationDir
    }

    def repository_snapshots = "https://oss.sonatype.org/content/repositories/snapshots/"
    def repository_releases = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

    apply plugin: "maven-publish"
    apply plugin: "signing"
    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = project.name
                    description = project.description
                    url = "https://github.com/peacetrue/${project.name}"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "xiayouxue"
                            name = "xiayouxue"
                            email = "xiayouxue@hotmail.com"
                        }
                    }
                    scm {
                        connection = "https://github.com/peacetrue/${project.name}.git"
                        developerConnection = "https://github.com/peacetrue/${project.name}.git"
                        url = "https://github.com/peacetrue/peacetrue/${project.name}"
                    }
                }

                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }

        repositories {
            maven {
                url version.endsWith("SNAPSHOT") ? repository_snapshots : repository_releases
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

}







